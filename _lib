#compdef lib

# We don't want to show the descriptions.
zstyle ':completion:*:*:lib:*:*' verbose no


# Get the path to the library.
local libpath=$(lib where)


# Completion for top-level commands.
_lib_cmds() {
  local subcmds=('open:open' 'add:add' 'grep:grep' 'ln:ln' 'index:index' \
                 'compile:compile' 'where:where')
  _describe 'command' subcmds
}


_lib_open() {
  # Complete files found in the archive.
  # -/ only displays directories
  # -S supressed the trailing /
  _path_files -W $libpath/archive -/ -S ''
}


# We require an initial command, potentially followed by some arguments.
_arguments -C '1: :_lib_cmds' ':arg1:->arg1' ':arg2:->arg2' '*::args:->args'


# Do completion for individual commands.
case $state in
  (arg1)
    case $line[1] in
      (open|ln)
        _lib_open
        ;;
      (add)
        _files -g "*.pdf"
        ;;
    esac
    ;;
  (arg2)
    case $line[1] in
      (open)
        case $line[2] in
          (-b|--bib|--bibtex)
            _lib_open
            ;;
        esac
        ;;
      (add)
        _files -g "*.bib"
        ;;
    esac
    ;;
esac
